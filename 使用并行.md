# 使用并行
Cython支持通过`cython.parallel`模块进行本地并行。使用这种并行方式必须释放GIL（请看[释放GIL](https://cython.readthedocs.io/en/latest/src/userguide/external_C_code.html#nogil)）。该模块目前支持OpenMP，之后可能会支持更多种类的后端。

<table><tr><td bgcolor=#AFAFAF><b>注意：</b>由于OpenMP的限制，该模块中的功能可能只能从主线程或并行区中调用。</td></tr></table>

cython.parallel.__prange__(_[start,] stop[, step][, nogil=False][, schedule=None[, chunksize=None]][, num_threads=None]_)    
这个函数被用于并行循环。OpenMP会自动开始一个线程池，并且根据使用的`schedule`参数分配工作。

线程局部性和减少量会自动推断为变量。

如果你在一个`prange`块中分配一个变量，这个变量会成为一个持续私有量，也就是说这个变量会包含从上一次迭代中得到的值。如果你在一个变量上使用了一个原地操作符，它会成为一个减少量，也就是说变量的线程局部副本的值将随操作符一起减少，并在循环后分配给原始变量。索引变量始终是持续私有量。与块并行分配的变量在块之后将是私有的并且不可用，因为没有顺序最后值的概念。（这一段完全没看懂，希望大神解惑）

参数：
* start – 表示循环开始位置的索引（与`range`的`start`参数相同）。
* stop – 表示循环结束位置的索引（与`range`的`stop`参数相同）。
* step – 表示序列步长的整数（与`range`的`step`参数相同）。不能是0。
* nogil – 该函数在使用时GIL必须被释放。如果`nogil`是`True`，则循环将包裹在`nogil`部分中。
* schedule – `schedule`参数被传给OpenMP，它可以是以下的一个值：

   * static：
            
         如果chunksize被给出，迭代会把大小为给定chunksize的块提前分发到所有线程。如果没有给出chunksize，则迭代空间被划分为大小近似相等的块，并且至多一个块被预先分配给每个线程。
         在调度开销比较重要且问题可以被缩减为数个运行时间大致相当的具有相同大小的块时，这种方式比较适用。
   * dynamic：

         迭代会在线程请求时以默认为1的chunksize分配给它们。
         当每个块的运行时间不一致且提前不可知，而且有大量的小块时，这种方式比较适合，因为这样可以使所有线程保持忙碌。

   * guided：
            
         与dynamic类似，迭代会在线程请求时分配给它们，但块的大小会逐渐减小。每个块的大小会与（未分配的迭代数除以参与的线程数的值）成比例，并渐渐降为1（或给定的chunksize）。
         相比dynamic，它的优势在于，dynamic下若最后一个块需要比预期更长的时间，或调度得不太好，在最后一个块运行时可能大多数线程都处于空闲状态，只有少量线程会工作。
   * runtime：
            
         调度和块大小取自运行时调度变量，可以通过openmp.omp_set_schedule()函数调用或OMP_SCHEDULE环境变量来设置。注意这实际上禁用了调度代码本身的任何静态编译时优化，因此可能显示比在编译时静态配置相同调度策略时稍差的性能。默认计划是实现定义的。有关更多信息，请参阅OpenMP规范[1]。

* num_threads – `num_threads`参数表示应该包含多少个线程。如果该参数未给出，OpenMP将会决定要使用多少个线程。一般来说这个值是机器上可以得到的核数。同时，该值也可以通过`omp_set_num_threads()`函数或`OMP_NUM_THREADS`环境变量来控制。 
* chunksize – `chunksize`参数表示用于在线程之间划分迭代的块的大小。只有`schedule`参数是`static`、`dynamic`或`guided`时该参数有效，且该参数并不必要。根据调度、负载平衡，调度开销和错误共享的数量（如果有的话），不同的chunksize会给出显著不同的性能结果。

减少量的例子：
```cython
from cython.parallel import prange

cdef int i
cdef int n = 30
cdef int sum = 0

for i in prange(n, nogil=True):
    sum += i

print(sum)
```
带有类型话内存视图（例如一个NumPy数组）的例子：
```cython
from cython.parallel import prange

def func(double[:] x, double alpha):
    cdef Py_ssize_t i

    for i in prange(x.shape[0]):
        x[i] = alpha * x[i]
```
cython.parallel.__parallel__(_num_threads=None_)

该指令可以用作`with`语句的一部分，用来并行执行代码序列。这对于设置`prange`使用的线程局部缓冲区非常有用。包含的`prange`将是一个不并行的工作共享循环，因此在并行部分中分配给的任何变量对于`prange`也是私有的。在并行块之后，并行块中的私有变量不可用。

线程局部缓冲区的例子：
```cython
from cython.parallel import parallel, prange
from libc.stdlib cimport abort, malloc, free

cdef Py_ssize_t idx, i, n = 100
cdef int * local_buf
cdef size_t size = 10

with nogil, parallel():
    local_buf = <int *> malloc(sizeof(int) * size)
    if local_buf is NULL:
        abort()

    # 以顺序循环填充局部缓冲区
    for i in xrange(size):
        local_buf[i] = i * 2

    # 使用线程局部缓冲区共享工作
    for i in prange(n, schedule='guided'):
        func(local_buf)

    free(local_buf)
```
cython.parallel.__threadid__()

返回线程的id。对于n个线程来说，id的范围是0到n-1。
## 编译
要使用OpenMP支持，你需要告诉C或C++编译器启用OpenMP。对于gcc来说，可以使用如下的setup.py文件来完成这一步：
```cython
from distutils.core import setup
from distutils.extension import Extension
from Cython.Build import cythonize

ext_modules = [
    Extension(
        "hello",
        ["hello.pyx"],
        extra_compile_args=['-fopenmp'],
        extra_link_args=['-fopenmp'],
    )
]

setup(
    name='hello-parallel-world',
    ext_modules=cythonize(ext_modules),
)
```
对Microsoft Visual C++编译器，使用'/openmp'来代替'-fopenmp'。
## 跳出循环
prange并行块在`nogil`模式下支持break、continue和return语句。并且，它允许在代码块中使用`with gil`代码块，还可以从中引发异常。但是，由于代码块使用OpenMP，不能放着不管，所以最好是退出进程。对于prange()来说，这意味着整个循环部分在任意线程的顺序迭代中第一次跳出、返回或触发异常之后被跳过。但是，在多个不同的值可能被返回的情况下，由于迭代没有特定的顺序，哪个值会被返回是不确定的。
```cython
from cython.parallel import prange

cdef int func(Py_ssize_t n):
    cdef Py_ssize_t i

    for i in prange(n, nogil=True):
        if i == 8:
            with gil:
                raise Exception()
        elif i == 4:
            break
        elif i == 2:
            return i
```
在上面这个例子中，到底是会引发一个异常，还是单纯跳出循环，抑或是返回2是不确定的。
## 使用OpenMP的函数
可以通过cimport openmp来使用OpenMP的函数：
```cython
# tag: openmp
# You can ignore the previous line.
# It's for internal testing of the Cython documentation.

from cython.parallel cimport parallel
cimport openmp

cdef int num_threads

openmp.omp_set_dynamic(1)
with nogil, parallel():
    num_threads = openmp.omp_get_num_threads()
    # ...
```